
const source = `
// Worms from "Digiverse" by Fairlight (Structures part only)
// https://www.pouet.net/prod.php?which=76719
// Adapted for Shader Sequencer with audio reactivity.

#define FAR 10.
#define FOV 80.0
#define FOG .4

#define PI 3.14159265
#define TAU (2.*PI)
#define PHI (1.618033988749895)

vec3 light = vec3(0.0);
vec3 opRep( vec3 p, vec3 c )
{
    return mod(p,c)-0.5*c;
}

vec3 opU2( vec3 d1, vec3 d2 ) {
    if (d1.x < d2.x) return d1;
    return d2;
}

vec3 opS2( vec3 d1, vec3 d2 )
{	
    if (-d2.x > d1.x) return -d2;
    return d1;
}

float vmax(vec3 v) {
	return max(max(v.x, v.y), v.z);
}

// Box: correct distance to corners
float fBox(vec3 p, vec3 b) {
	vec3 d = abs(p) - b;
	return length(max(d, vec3(0))) + vmax(min(d, vec3(0)));
}

float fCross(vec3 p, vec3 size) {
	return min(fBox(p, size), min(fBox(p, size.zxy), fBox(p, size.yzx)));
}

// scene
vec3 map(vec3 p) {
    vec3 op = p;
    vec3 obj = vec3(0, 1., 1.0);
    vec3 orgP = p;
 
    p = opRep(orgP, vec3(.5));
    
    vec3 size = vec3(0.2, .32, .1 );
    
    #define C size *= 1.1; p = opRep(orgP, vec3(0.35) + size.y + size.z); obj = opS2(obj, vec3(fCross(p, size) + .05, 0.0, 1.));
    
    C C C
    
    return obj;
}

vec3 trace(vec3 ro, vec3 rd) {
    vec3 t = vec3(0., -1., 0.0), d;
    for (int i = 0; i < 70; i++) {
        d = map(ro + rd * t.x);
        if (abs(d.x) < 0.001 || t.x > FAR) break;
        t.x += d.x * .7; 
    }
    t.yz = d.yz;
    return t;
}

vec3 traceRef(vec3 ro, vec3 rd) {
    vec3 t = vec3(0., 1., 0.), d;

    for (int i = 0; i < 36; i++) {
        d = map(ro + rd * t.x);
        if (abs(d.x) < 0.001 || t.x> FAR) break;
        t.x += d.x;
    }
    t.yz = d.yz;
    return t;
}

float softShadow(vec3 ro, vec3 lp, float k) {
    const int maxIterationsShad = 18;
    vec3 rd = (lp - ro);

    float shade = 1.0;
    float dist = .01;
    float end = max(length(rd), 0.001);
    float stepDist = end / float(maxIterationsShad);

    rd /= end;
    for (int i = 0; i < maxIterationsShad; i++) {
        float h = map(ro + rd * dist).x;
        shade = min(shade, smoothstep(0.0, 1.0, k * h / dist)); 
        dist += min(h, stepDist * 2.); 
        if (h < 0.001 || dist > end) break;
    }
    return min(max(shade, 0.55), 1.0);
}

vec3 getNormal(in vec3 pos) {
    vec2 eps = vec2(0.001, 0.0);
    vec3 normal = vec3(
        map(pos + eps.xyy).x - map(pos - eps.xyy).x,
        map(pos + eps.yxy).x - map(pos - eps.yxy).x,
        map(pos + eps.yyx).x - map(pos - eps.yyx).x);
    return normalize(normal);
}

float getAO(in vec3 hitp, in vec3 normal) {
    float dist = .05;
    vec3 spos = hitp + normal * dist;
    float sdist = map(spos).x;
    return clamp(sdist / dist, 0.4, 1.0);
}

vec3 getObjectColor(vec3 p, vec3 n, vec2 mat, float time_in) {
    // Mids affect blue color
    return vec3(.0, .0, .1) + vec3(0., 1.0, 1.0 + iAudio.y * 0.5) * smoothstep(0.1, .0, fract(p.y * 9.));
}

vec3 doColor( in vec3 sp, in vec3 rd, in vec3 sn, in vec3 lp, vec2 mat, float time_in) {
	vec3 ld = lp - sp; 
    float lDist = max(length(ld), 0.001);
    ld /= lDist; 

    float atten = 2.0 / (1.0 + lDist * 0.525 + lDist * lDist * 0.05);
	float diff = max(dot(sn, ld), .1);
    float spec = pow(max(dot(reflect(-ld, sn), -rd), 0.0), 1.0);

    vec3 objCol = getObjectColor(sp, sn, mat, time_in);
    
    // Highs affect specular highlight color
    return (objCol * (diff + 0.15) + vec3(.1 + iAudio.z * 0.4, .1, .1) * spec * .8) * atten;
}

void mainImage(out vec4 fragColor, in vec2 fragCoord) {
    
    // Overall volume affects animation speed
    float t = iTime * (1.0 + iAudio.w * 0.5);

    vec2 uv = fragCoord.xy / iResolution.xy - .5;
    
    uv *= tan(radians (FOV) / 2.0) * 2.;
    uv.y += sin(t * 3. + cos(4.*-t)) * 0.03;
    
    float 
        sk = sin(t * .3) * 22.0,
        ck = cos(t * .3) * 22.0,
        
        mat = 0.;
        
    light = vec3(0., 1., 1.);        
    
    vec3 sceneColor = vec3(0.);
    
    vec3 
        vuv = normalize(vec3(0., 1., sin(iTime) * .3)), // Note: raw iTime for up-vector wobble
    	ro = vec3(t * .3 , 0.4 , 5.12 ), 
        oro,
    	vrp =  vec3(t * .3 - 18. + ck, 0.4, -43. + sk ),
		
    	vpn = normalize(vrp - ro),
    	u = normalize(cross(vuv, vpn)),
    	v = cross(vpn, u),
    	vcv = (ro + vpn),
    	scrCoord = (vcv + uv.x * u * iResolution.x/iResolution.y + uv.y * v),
    	rd = normalize(scrCoord - ro);
                

    vec3 lp = light + ro;

    vec3 tr = trace(ro, rd), otr = tr;    
    
    float fog = smoothstep(FAR * FOG, 0., tr.x * 3.);
    
    ro += rd * tr.x;
    float d = tr.x;

    vec3 sn = getNormal(ro);	
    float ao = getAO(ro, sn);
    
    sceneColor += doColor(ro, rd, sn, lp, tr.yz, iTime) * 4.;
    float sh = softShadow(ro, lp, 1.);
    
    rd = reflect(rd, sn);
    
    tr = traceRef(ro + rd * .015, rd);
	ro += rd * tr.x;
    
    sn = getNormal(ro);
   
    sceneColor += doColor(ro, rd, sn, lp, tr.yz, iTime);        
    sceneColor *= sh * fog * ao;

    fragColor = vec4(clamp(sceneColor, 0.0, 1.0), d);
}
`;
export default source;